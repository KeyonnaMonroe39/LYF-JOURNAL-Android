plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
}

android {
    compileSdkVersion 31
    // construct version name
    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 0
    def code = 1
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 31
        versionCode code
        // not set as String because versionName is not defined in buildTypes
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        // test options
        testOptions.unitTests.includeAndroidResources = true
        // test runner and consumer rules
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        // there are two buildTypes {debug, release}. The only difference between debug
        // and release buildTypes is that code debug logging is enabled in debug environment,
        // otherwise logging is disabled.
        debug {
            buildConfigField "boolean", "DEBUG_MODE", "true"
            buildConfigField "String", "VERSION_NAME", "\"${defaultConfig.versionName}\""
            buildConfigField "String", "VERSION_CODE", "\"${code}\""
            manifestPlaceholders = [build_version: "${versionMajor}.${versionMinor}.${versionPatch}"]
            // test coverage
            testCoverageEnabled true
        }
        release {
            buildConfigField "boolean", "DEBUG_MODE", "false"
            buildConfigField "String", "VERSION_NAME", "\"${defaultConfig.versionName}\""
            buildConfigField "String", "VERSION_CODE", "\"${code}\""
            manifestPlaceholders = [build_version: "${versionMajor}.${versionMinor}.${versionPatch}"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {
    // youtube player
    implementation 'com.google.apis:google-api-services-youtube:v3-rev183-1.22.0'

    // gson
    implementation 'com.google.code.gson:gson:2.9.0'

    // okHttp
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.6'

    // work
    // [02.26.22] do not upgrade passed 2.7.1. This causes "test events were not received"
    // Tried with upper versions 2.7.2. Maintain current version until there is a fix.
    def work = '2.7.1'
    implementation "androidx.work:work-runtime-ktx:$work"

    // androidx
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-process:2.4.1'
    def room = "2.4.2"
    implementation "androidx.room:room-runtime:$room"
    kapt "androidx.room:room-compiler:$room"

    // annotation
    implementation 'androidx.annotation:annotation:1.3.0'

    // coroutines
    def coroutines = '1.6.0-native-mt'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    // unit tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}